plugins {
    id 'com.jfrog.artifactory' version '3.1.1'
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply from: 'artifactory-template.gradle'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()    
    maven { url 'http://jartifacts.longlinkislong.com/artifactory/libs-snapshot' }
    maven { url 'http://jartifacts.longlinkislong.com/artifactory/libs-release' }
}

def getSnapshot(projectName) {
    def result = new ProcessBuilder('sqlite3', 'versioning.db', "select hash from versioning where library='$projectName'").start().text.trim()
    
    return "$result-SNAPSHOT"
}

def getVersion(projectName) {
    return getVersion(projectName, build)
}

def getVersion(projectName, buildType) {
    if (isJenkins.toBoolean()) {
        if (buildType == 'snapshot') {
            return getSnapshot(projectName)
        } else if (buildType == 'release') {
            return getRelease(projectName)
        } else {
            throw new IllegalStateException("Unsupported build type: $buildType")
        }
    } else {
        if (buildType == 'snapshot') {
            return "http://jenkins.longlinkislong.com/job/$projectName/lastSuccessfulBuild/artifact/build/version.snapshot".toURL().text.trim()
        } else if (buildType == 'release') {
            return "http://jenkins.longlinkislong.com/job/$projectName/lastSuccessfulBuild/artifact/build/version.release".toURL().text.trim()
        } else {
            throw new IllegalArgumentException("Unsupported build type: $buildType")
        }
    }
}

def getRelease(projectName) {
    def version = new ProcessBuilder('sqlite3', 'versioning.db', "select major, minor, revision from versioning where library='$projectName'").start().text.trim()
    
    if (version.empty) {
        System.err.println("Library [$projectName] was not found in database!")
        return null
    } else {
        return version.replace('|', '.')
    }
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.+'    
    compile "com.longlinkislong:gloop-spi:${getVersion('gloop-1.X.X-vectors')}"
    compile "org.lwjgl:lwjgl:3.0.0"
    compile "org.lwjgl:lwjgl-platform:3.0.0:natives-windows"
    compile "org.lwjgl:lwjgl-platform:3.0.0:natives-linux"
    compile "org.lwjgl:lwjgl-platform:3.0.0:natives-osx"
}
